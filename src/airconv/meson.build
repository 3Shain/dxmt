airconv_src = files([
 'airconv_context.cpp',
 'air_type.cpp',
 'air_signature.cpp',
 'air_operations.cpp',
 'dxbc_converter.cpp',
 'dxbc_converter_gs.cpp',
 'dxbc_converter_ts.cpp',
 'dxbc_converter_basicblock.cpp',
 'dxbc_converter_cfg.cpp',
 'dxbc_instructions.cpp',
 'dxbc_signature.cpp',
 'metallib_writer.cpp',
 'nt/air_builder.cpp',
 'nt/dxbc_converter_base.cpp',
 'transforms/lower_16bit_texread.cpp',
])

airconv_cli_src = files(['airconv_cli.cpp'])

# generated by llvm-config --libs bitwriter passes
llvm_deps = [
 '-lLLVMPasses' ,'-lLLVMTarget' ,'-lLLVMObjCARCOpts' ,
 '-lLLVMCoroutines', '-lLLVMipo', '-lLLVMInstrumentation',
 '-lLLVMVectorize', '-lLLVMLinker', '-lLLVMIRReader', 
 '-lLLVMAsmParser',
 '-lLLVMFrontendOpenMP', '-lLLVMScalarOpts', '-lLLVMInstCombine', 
 '-lLLVMAggressiveInstCombine', '-lLLVMTransformUtils', 
 '-lLLVMBitWriter', '-lLLVMAnalysis', '-lLLVMProfileData', 
 '-lLLVMSymbolize', '-lLLVMDebugInfoPDB', '-lLLVMDebugInfoMSF', 
 '-lLLVMDebugInfoDWARF', '-lLLVMObject', '-lLLVMTextAPI', '-lLLVMMCParser', 
 '-lLLVMMC', '-lLLVMDebugInfoCodeView',
 '-lLLVMBitReader', '-lLLVMCore', '-lLLVMRemarks', '-lLLVMBitstreamReader', 
 '-lLLVMBinaryFormat', '-lLLVMSupport', '-lLLVMDemangle'
]

llvm_cxx_flags = [
 '-fno-exceptions','-funwind-tables','-fno-rtti','-D_FILE_OFFSET_BITS=64',
 '-D__STDC_CONSTANT_MACROS','-D__STDC_FORMAT_MACROS','-D__STDC_LIMIT_MACROS'
]

llvm_ld_flags = [
 '-L'+join_paths(meson.project_source_root(), 'toolchains/llvm/lib')
]

airconv_forward_dep = declare_dependency(
  include_directories : [ dxmt_include_path, include_directories('.') ],
)

airconv_shaders = [
  'shaders/air_msad.metal',
  'shaders/air_samplepos.metal',
  'shaders/air_tessellation.metal',
]

airconv_shaders_ir = metalir_generator.process(
  airconv_shaders,
  extra_args: ['-std=metal3.1', '--target=air64-apple-macos14.0'],
)
airconv_shaders_header = hexdump_generator.process(airconv_shaders_ir)

airconv_src += airconv_shaders_header

build_airconv_for_windows = get_option('build_airconv_for_windows')

if build_airconv_for_windows

llvm_include_path = include_directories('../../toolchains/llvm/include') # FIXME: in favor of path relative to project

airconv_lib = static_library('airconv', airconv_src,
  include_directories : [ dxmt_include_path, llvm_include_path ],
  cpp_args            : [ llvm_cxx_flags, '-DWIN_EXPORT' ],
  dependencies        : [ dxbc_parser_dep ],
  link_args           : llvm_ld_flags
)

airconv_dep = declare_dependency(
  link_with           : [ airconv_lib ],
  include_directories : [ dxmt_include_path, include_directories('.') ],
  link_args           : [ llvm_ld_flags, llvm_deps ] # meh
)

lib_d3dcompiler = cpp.find_library('d3dcompiler_47')

executable('airconv', airconv_src + airconv_cli_src,
  include_directories : [ dxmt_include_path, llvm_include_path ],
  cpp_args            : [ llvm_cxx_flags, '-DWIN_EXPORT' ],
  dependencies        : [ dxbc_parser_dep, lib_d3dcompiler ],
  link_args           : [ llvm_ld_flags, llvm_deps ]
)

endif

if cpu_family == 'x86_64' or dxmt_native
  subdir('darwin')
endif